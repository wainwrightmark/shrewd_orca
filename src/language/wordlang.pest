file = {SOI ~ question ~ EOI}
WHITESPACE = _{ " "| "\t" | NEWLINE }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
question = {equation | expression}
equation = {expression ~ equality_operator ~ expression}
expression = {many_expression | fixed_length_expression }
fixed_length_expression = { word_query_conjunction*}
many_expression = {many_term ~ ("+" ~ query_term)* }
word_query_conjunction = {word_query_disjunction ~("+" ~ word_query_disjunction)*}
word_query_disjunction = {query_term ~("/" ~ query_term)*}
bracketed_conjunction = {"(" ~ word_query_conjunction ~ ")"}
query_term = { pattern | literal |  any | range | length | tag | bracketed_conjunction  } //manyany
literal = @{ASCII_ALPHA+}
question_marks = @{"?"+}
tag = ${"#" ~ literal}
pattern = ${
&(any? ~ (literal | question_marks | character_class) ) ~ 
&(literal? ~ (any | question_marks | character_class) ) ~ 
(literal | question_marks | any | character_class )+}
many_any = {"**"}
many_tag = ${"!" ~ literal}
many_term = {many_any | many_tag}
any = @{"*"+}
character_class = ${"@" ~ ASCII_ALPHA}
length = @{ASCII_DIGIT+}
range = ${length ~ ".." ~ length}
equality_operator = ${ "=" ~ ASCII_ALPHA}
